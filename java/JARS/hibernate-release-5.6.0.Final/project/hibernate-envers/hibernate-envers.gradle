/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or <http://www.gnu.org/licenses/lgpl-2.1.html>.
 */

description = 'Hibernate\'s entity version (audit/history) support'

apply from: rootProject.file( 'gradle/published-java-module.gradle' )
apply plugin: 'hibernate-matrix-testing'

dependencies {
    compile( project( ':hibernate-core' ) )

    // TODO HHH-13703: get rid of this dependency
    compile( libraries.dom4j )

    provided( libraries.ant )
    annotationProcessor( project( ':hibernate-jpamodelgen' ) )

    testCompile( project( ':hibernate-testing' ) )
    testCompile( project( path: ':hibernate-core', configuration: 'tests' ) )
}

sourceSets {
    test {
        ext {
            enversDemoJavaDir = file( "src/demo/java" )
            enversDemoResourcesDir = file( "src/demo/resources" )
        }
        java {
            srcDir enversDemoJavaDir
        }
        resources {
            srcDir enversDemoResourcesDir
        }
    }
}

configurations {
    tests {
        description = 'Configuration for the produced test jar'
    }
}

jar {
    manifest {
        attributes(
                // BND Plugin instructions (for OSGi):
                'Import-Package': [
                        // TODO: Shouldn't have to explicitly list the JPA packages, but
                        // the plugin generates them with [1.0,2) versions.
                        "javax.persistence;version=\"${project.jpaVersion.osgiName}\"",
                        "javax.persistence.criteria;version=\"${project.jpaVersion.osgiName}\"",
                        "javax.persistence.metamodel;version=\"${project.jpaVersion.osgiName}\"",
                        "javax.persistence.spi;version=\"${project.jpaVersion.osgiName}\"",
                        // optionals
                        'javax.naming;resolution:=optional',
                        'org.apache.tools.ant;resolution:=optional',
                        // Temporarily support JTA 1.1 -- Karaf and other frameworks still
                        // use it.  Without this, the plugin generates [1.2,2).
                        'javax.transaction;version="[1.1,2)"',
                        // Also import every package referenced in the code
                        '*'
                ].join( ',' )
        )
    }
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task testJar(type: Jar, dependsOn: testClasses) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier.set( 'test' )
    from sourceSets.test.output
}

artifacts {
    tests testJar
}

tasks."matrix_mariadb" {
    beforeTest { descriptor ->
        println "Starting test: " + descriptor
    }
}
