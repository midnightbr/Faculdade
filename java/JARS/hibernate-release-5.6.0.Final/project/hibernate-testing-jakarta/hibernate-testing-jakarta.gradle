/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or <http://www.gnu.org/licenses/lgpl-2.1.html>.
 */

description = 'Support for testing Hibernate ORM Jakarta functionality'

apply from: rootProject.file( 'gradle/published-java-module.gradle' )

configurations {
	jakartaeeTransformJars
}

dependencies {
	compile project( ':hibernate-core-jakarta' )
	compile( libraries.jakarta_jta )
	compile( libraries.junit )
	compile( libraries.byteman )
	compile( libraries.byteman_install )
	compile( libraries.byteman_bmunit )
	compile( libraries.xapool )
	compile( libraries.log4j2 )
	compile( libraries.jboss_tx_spi_jakarta ) {
		transitive=false;
	}
	compile ( libraries.jboss_jta_jakarta ) {
		transitive=false;
	}

	jakartaeeTransformJars 'biz.aQute.bnd:biz.aQute.bnd.transform:5.1.1',
			'commons-cli:commons-cli:1.4',
			'org.slf4j:slf4j-simple:1.7.30',
			'org.slf4j:slf4j-api:1.7.26',
			'org.eclipse.transformer:org.eclipse.transformer:0.2.0',
			'org.eclipse.transformer:org.eclipse.transformer.cli:0.2.0'
	testCompile fileTree(dir: 'libs', include: '*.jar')
}

jar {
	mustRunAfter project(':hibernate-testing').tasks.jar
	dependsOn project(':hibernate-testing').tasks.jar
	def baseDir = project(':hibernate-testing').buildDir
	def baseJars = fileTree(baseDir).matching {include 'libs/*.jar' }
	inputs.files(baseJars).skipWhenEmpty()
	outputs.dir project.buildDir
	doLast {
		new File(project.buildDir, "libs").mkdirs()
		fileTree(project.buildDir).matching { include 'libs/*.jar' }.each { delete it }

		baseJars.each { bundleJar ->
			def sourceJarPath = baseDir.path + '/libs/' + bundleJar.name
			println 'Initial bundle jar name [ ' + sourceJarPath + ' ]'

			def finalBundleJarName = project.buildDir.path + '/libs/' + bundleJar.name.replaceAll( 'hibernate-testing', 'hibernate-testing-jakarta' )
			println 'Default jakarta final bundle jar name [ ' + finalBundleJarName + ' ]'

			def transformerArgs = [
					sourceJarPath, finalBundleJarName,
					'-q', // quiet output
					'-tr', new File(getProjectDir().getParentFile(), 'rules/jakarta-renames.properties').path,
					'-tv', new File(getProjectDir().getParentFile(), 'rules/jakarta-versions.properties').path,
					'-td', new File(getProjectDir().getParentFile(), 'rules/jakarta-direct.properties').path,
			]

			println 'Transformer options:'
			transformerArgs.each {
				println '  [ ' + it + ' ]'
			}

			javaexec {
				classpath configurations.jakartaeeTransformJars
				main = 'org.eclipse.transformer.jakarta.JakartaTransformer'
				args = transformerArgs
			}
		}
	}
}
